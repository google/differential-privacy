#
# Copyright 2021 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//rules:write_file.bzl", "write_file")

config_setting(
    name = "linux_x86_64",
    constraint_values = [
        "@platforms//os:linux",
        "@platforms//cpu:x86_64",
    ],
)

config_setting(
    name = "macos_x86_64",
    constraint_values = [
        "@platforms//os:macos",
        "@platforms//cpu:x86_64",
    ],
)

write_file(
    name = "unsupported_platform_script",
    out = "unsupported_platform_script.sh",
    content = [
        "echo Error: This platform is not supported",
    ],
)

filegroup(
    name = "sample_data_files",
    srcs = glob(["data/*.csv"]),
)

sh_binary(
    name = "execute_query",
    srcs = select({
        ":linux_x86_64": ["@zetasql_execute_query_linux//file"],
        ":macos_x86_64": ["@zetasql_execute_query_macos//file"],
        "//conditions:default": [":unsupported_platform_script"],
    }),
    args = [
        "--enabled_language_features=" + ",".join([
            "MAXIMUM",
            "+differential_privacy",
            "+differential_privacy_thresholding",
            "+differential_privacy_public_groups",
            "+differential_privacy_min_privacy_units_per_group",
            "+differential_privacy_nested",
            "+differential_privacy_per_aggregation_budget",
        ]),
        "--table_spec='" + ",".join([
            "day_data=csv:data/day_data.csv",
            "outliers_week_data=csv:data/outliers_week_data.csv",
            "week_data=csv:data/week_data.csv",
        ]) + "'",
    ],
    data = [
        ":sample_data_files",
    ],
)
